import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
//    id 'kotlin-kapt'
    id 'com.google.devtools.ksp'
    id 'dagger.hilt.android.plugin'
    id 'io.gitlab.arturbosch.detekt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.digitalsolution.familyfilmapp'
    compileSdk 34

    defaultConfig {
        applicationId "com.digitalsolution.familyfilmapp"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {

        debug {
            buildConfigField "String", "WEB_ID_CLIENT", "\"${WEB_ID_CLIENT}\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "WEB_ID_CLIENT", "\"${WEB_ID_CLIENT}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.8'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
//    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.22')

    // Androidx
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.7.0-alpha02"

    // Splash
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Navigation
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation "androidx.navigation:navigation-compose:2.7.2"

    // Compose
    implementation platform('androidx.compose:compose-bom:2023.09.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation "androidx.compose.material:material-icons-extended:1.5.1"

    //Google auth
    implementation("com.google.android.gms:play-services-auth:20.7.0")

    // Hilt
    implementation "com.google.dagger:hilt-android:2.48"
    ksp "com.google.dagger:hilt-android-compiler:2.48"
    kspTest "com.google.dagger:hilt-android-compiler:2.48"

    // Firebase
    implementation(platform('com.google.firebase:firebase-bom:32.2.3'))
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Coil
    implementation "io.coil-kt:coil:2.2.2"
    implementation 'io.coil-kt:coil-compose:2.4.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.09.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}


// Add detekt configuration
detekt {
    buildUponDefaultConfig = false // preconfigure defaults
    allRules = true // activate all available (even unstable) rules.
    config = files("$projectDir/config/detekt.yml")
    // point to your custom config defining rules to run, overwriting default behavior
    basePath = rootProject.projectDir.absolutePath
    baseline = file("$projectDir/config/baseline.xml") // a way of suppressing issues before introducing detekt
}

tasks.withType(Detekt).configureEach {
    reports {
        html.required.set(true) // observe findings in your browser with structure and code snippets
        xml.required.set(true) // checkstyle like format mainly for integrations like Jenkins
        txt.required.set(true)
        // similar to the console output, contains issue signature to manually edit baseline files
        sarif.required.set(true)
        // standardized SARIF format (https://sarifweb.azurewebsites.net/) to support integrations with GitHub Code Scanning
        md.required.set(true) // simple Markdown format
    }
}

tasks.withType(Detekt).configureEach {
    jvmTarget = JavaVersion.VERSION_17
}

tasks.withType(DetektCreateBaselineTask).configureEach {
    jvmTarget = JavaVersion.VERSION_17
}